
channelBits:    .db 1, 2, 4, 8, 1, 2, 4, 8

outputTrianglePeriod:
    ldx curStream
    cpx #4          ; get offset for pitch vars
    bcc +
    dex
+   lda nesmus.periodL, x
    sta $400A
    lda nesmus.periodH, x
    ora #$80
    sta $400B
    ldx curStream
    rts

outputSweep:
    cpx #4
    bcc +
    dex
    dex
+   lda nesmus.pulseSweep, x
    bmi @checkPrev  ; if sweep is disabled at all, output $0F (sweep up, speed 0, shift 7)
    lda #$0F
@checkPrev
    ldx curChannel ;3
    cmp nesmus.prevPulseSweep, x ;4
    beq outputPulsePeriod ;2
    sta nesmus.prevPulseSweep, x ;5
@writeSweep
    sta $4001, y

outputPulsePeriod:
    ldx curStream                   ; get offset for pitch vars
    cpx #4          
    bcc +
    dex
    
+   ldy curChannel                  
    lda nesmus.prevPulseSweep, y
    bpl @checkPeriodH               ; if hardware sweep disabled, continue
    ldy curStream                   ; else check if new note started
    lda newNoteFlags
    and streamBits.w, y
    beq @done                       ; if not, do nothing
    lda #$0F                        ; invalidate previous high period
    ldy curChannel
    sta nesmus.prevPeriodH, y
    lda nesmus.periodH, x           ; load high period and branch to output
    jmp @writePeriodH
    
@checkPeriodH                 
    lda nesmus.periodH, x           ; get high period
    and #7
    cmp nesmus.prevPeriodH, y       ; skip writing if not changed
    beq @writePeriodL
    sta nesmus.prevPeriodH, y
@writePeriodH
    ldy outputOffs
    ora #$80
    sta $4003, y
@writePeriodL
    ldy outputOffs
    lda nesmus.periodL, x           ; else, write low period if changed
    sta $4002, y
@done    
    ldx curStream
    rts
    
outputNoisePeriod:
    ldx curStream
    cpx #7
    bne @drum
    lda nesmus.curNote+7
    and #$0F
@writeNoise
    eor #$0F
    sta $400E
    lda #$80
    sta $400F
    bne outputPulsePeriod@done
@drum
    lda nesmus.envValue+2
    lsr
    lsr
    lsr
    lsr
    jmp @writeNoise
    
muteChannel:
    cpy #8
    beq muteTriangle
    lda #$30
    sta $4000, y
    rts
muteTriangle:
    lda #0
    sta $4008
    lda #$87
    sta $400B
    rts

outputStream:
    ldy outputOffs
    lda nesmus.streamFlags, x
    bmi @checkMask
@muteStream
    cpx #4
    bcc muteChannel
@return
    rts
    
; check if there is already output on this channel. skip this one if yes.
@checkMask
    lda channelBits.w, x
    and outputMask
    bne @return
    lda nesmus.streamFlags, x
    and #3
    beq @muteStream
    
; set flag for this channel in output mask
+   lda channelBits.w, x
    ora outputMask
    sta outputMask
; branch for triangle channel.
    cpy #8
    beq outputTriangle
    bcs outputNoise
; output pulse channels    
    jsr outputVolume
    jmp outputSweep
    
outputNoise:
    jsr outputVolume
    jmp outputNoisePeriod
    
outputTriangle:
; mute channel if note off or stream volume == 0 or note volume == 0
; play sound otherwise
    lda nesmus.streamVolume, x
    beq muteTriangle
    lda nesmus.noteVolume, x
    beq muteTriangle
    lda nesmus.streamFlags, x
    and #3
    beq muteTriangle
    lda #$FF
    sta $4008
    jmp outputTrianglePeriod
    
    
-   jmp muteChannel
outputVolume:
    cpx #4
    bcs @sfx
@envelope
    cpx #3
    bne +
    dex
+   lda nesmus.envValue, x
    jmp applyGain
@sfx
    lda #$0F
    cpx #6
    bcs applyGain
    ora nesmus.sfxDuty-4, x
applyGain:
    sta tmpvar
    and #$0F
    beq -
    sta tmpvar+1
    lda nesmus.streamVolume, x
    clc
    adc nesmus.noteVolume, x
    beq -
    clc
    adc tmpvar+1
    sec
    sbc #30
    cmp #$10
    bmi +
@maxVolume
    lda #$0F
    bne @writeVolume
+   cmp #1
    bpl @writeVolume
    lda #1
@writeVolume
    sta tmpvar+1
    lda tmpvar
    and #$C0
    ora tmpvar+1
    ora #$30
    sta $4000, y
    rts
    
; volume = envValue - (15 - streamVol) - (15 - noteVol)
;        = envValue + streamVol + noteVol - 30
    